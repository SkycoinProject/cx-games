package mov

/////////////////////////////////////////////////////
/////////////////////////////////////////////////////
//////////////////////            ///////////////////
//////////////////////  MOVEMENT  ///////////////////
//////////////////////            ///////////////////
/////////////////////////////////////////////////////
/////////////////////////////////////////////////////

import "app"
import "entities"
import "glfw"
import "map"
import "inputs"
import "time"



var Force f32 = 4.0



// in, out (velocity) 
func GetPlayerVelocityFromInput (ivX f32, ivY f32) (ovX f32, ovY f32) {
	ovX = getVelocity(ivX, inputs.HoldingGoLeft(), inputs.HoldingGoRight())
	ovY = getVelocity(ivY, inputs.HoldingGoDown(), inputs.HoldingGoUp())
	//printf("ovX,ovY: %v,%v \n", ovX, ovY)
}


// t = entity type 
// i = entity id 
func ConstrainPosByMap (t i32, i i32) (x f32, y f32, velX f32, velY f32) {
	// collision bounce 
	var gridDelta i32 // for player crossing boundaries into adjacent grid cells 
	
	//		in X dimension 
	gridDelta, x, velX = BounceOffWalls(
		entities.All[t][i].X,
		entities.All[t][i].VelX,
		entities.All[t][i].ExtentX, 
		entities.All[t][i].CellMin.X, 
		entities.All[t][i].CellMax.X, 
		entities.All[t][i].ExitWest, 
		entities.All[t][i].ExitEast)
	
	if gridDelta != 0 {
		var x i32 = entities.All[t][i].GridPos.X + gridDelta
		var y i32 = entities.All[t][i].GridPos.Y
		map.SetPlayerRoomBounds(x, y, false)
	}

	//		in Y dimension 
	gridDelta, y, velY = BounceOffWalls(
		entities.All[t][i].Y, 
		entities.All[t][i].VelY, 
		entities.All[t][i].ExtentY, 
		entities.All[t][i].CellMin.Y, 
		entities.All[t][i].CellMax.Y, 
		entities.All[t][i].ExitSouth, 
		entities.All[t][i].ExitNorth) //-1.0D, 1.0D)

	if gridDelta != 0 {
		var x i32 = entities.All[t][i].GridPos.X
		var y i32 = entities.All[t][i].GridPos.Y + gridDelta
		map.SetPlayerRoomBounds(x, y, false)
	}
}


// args: 
// position, velocity, radius, negative edge boundary, positive edge boundary, 
// exit in negative direction, exit in positive direction 
// returns: 
// gridDelta is the change to the current index into the "Cells" grid array. 
// (for when player crosses the boundary into an adjacent cell) 
func BounceOffWalls (pos f32, vel f32, rad f32, negEdge f32, posEdge f32, negExit bool, posExit bool) (gridDelta i32, outPos f32, outVel f32) {
		
	outPos = pos
	outVel = vel

	if vel < 0.0 {
		if negExit {
			if pos < negEdge {
				str.print("passed [-] threshold")
				gridDelta = -1
			}
		}else{
			if (pos - rad) < negEdge {
				outPos = f32.add(negEdge, rad)
				outVel = f32.abs(vel)
			}
		}
	}else
	if vel > 0.0 {
		if posExit {
			if pos >= posEdge {
				str.print("passed [+] threshold")
				gridDelta = 1
			}
		}else{
			if (pos + rad) >= posEdge {
				outPos = f32.sub(posEdge, rad)
				outVel = f32.sub(0.0, f32.abs(vel))
			}
		}
	}
}


func dampenNegativeMovement (velocity f32, strength f32) (out f32) {
	out = f32.add(velocity, Force * time.Delta * strength)

	if out > 0.0 {
		out = 0.0
	}
}


func dampenPositiveMovement (velocity f32, strength f32) (out f32) {
	out = f32.sub(velocity, Force * time.Delta * strength)

	if out < 0.0 {
		out = 0.0
	}
}


func getVelocity (velocity f32, negative bool, positive bool) (out f32) {
	out = velocity

	//____________________________________________ 
	if positive { // _________ speed up positively 
		if out < 0.0 {
			out = dampenNegativeMovement(out, 4.0)
		}

		out = f32.add(out, Force * time.Delta)
	} else
	if negative { // _________ speed up negatively 
		if out > 0.0 {
			out = dampenPositiveMovement(out, 4.0)
		}

		out = f32.sub(out, Force * time.Delta)
	} else { //_________________________ slow down 
		if out > 0.0 {
			out = dampenPositiveMovement(out, 1.0)
		} else
		if out < 0.0 {
			out = dampenNegativeMovement(out, 1.0)
		}
	}
}

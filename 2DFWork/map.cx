/*

In our imagined world for this game, the universe is filled with solid
rock everywhere.

We have a cell grid of possibilities.  We carve out air/room space
in MOST of those cells.  Some will remain
solid rock, for a bit of variety in overall map shape.

I THINK OF THE Y DIMENSION INCREASING UPWARDS
	SO KEEP THIS IN MIND WITH geom.DIR_* (directions) 
	& THE "Cells" GRID INDEXING 
	
This is how it is by default in OpenGL space, Unity and many modern 3D
apps, so I think it's important to generally use this convention. 

*/

package map

import "gl"

import "app"
import "draw"
import "entities"
import "geom"



var NumCellsAcross i32 = 4 // in both dimensions 
var WTh            f32 = 0.085 // wall thickness 
var CellSpan       f32 = 2.0 / i32.f32(NumCellsAcross)
var CellSpanHalf   f32 = CellSpan / 2.0
var Cells          [4][4]Cell   // FIXME: use constants when we can 
var Carvs          []CarvedRoom // used/carved-out locations of the Cells grid 
var plr            i32 = entities.ENT_PLAYER // FIXME?: this is wrong to have in "map"?

type Cell struct {
	CarvsId i32 // MAX_I32 == solid rock 
	Exits    []i32 // initial possibilities that point to all valid grid slot neighbor cells 
}

type CarvedRoom struct {
	// insets (from room bounds) 
	/*
	Top    f32
	Right  f32
	Bottom f32
	Left   f32
	*/
	
	Pos    app.Vec2I
	Exits  []i32 // the actual room connections after map generation 
}



func Init () {
	str.print("map.Init ()")
	
	setupPossibleExits()
	carveOutConnectedRooms()
	var r i32 = i32.rand(0, len(Carvs))
	SetPlayerRoomBounds(Carvs[r].Pos.X, Carvs[r].Pos.Y, true)
}


/*
func GetNumCarvedOutRooms () (num i32) {
	num = len(Carvs)
}
func GetRoom (id i32) (room CarvedRoom) {
	room = Carvs[id]
}
func GetCarvsIdAt (x i32, y i32) (id i32) {
	id = Cells[y][x].CarvsId
}
func GetNumExitsAt (roomId i32) (num i32) {
	num = len(Carvs[roomId].Exits)
}
func GetExitAt (roomId i32, exitId i32) (num i32) {
	num = len(Carvs[roomId].Exits[exitId])
}
*/ 


func SetPlayerPos (pos app.Vec2) {
	entities.All[plr][0].x = pos.X
	entities.All[plr][0].y = pos.Y
}
func SetPlayerRoomBounds (x i32, y i32, putInCenter bool) {
	var roomId i32 = Cells[y][x].CarvsId

	printf("SetPlayerRoomBounds (roomId: %d) =============================\n", roomId)
	printf("    x,y: %d,%d \n", x, y)
	var room CarvedRoom = Carvs[roomId]
	entities.All[plr][0].GridPos = room.Pos
	//printf("set ball.GridPos: %d,%d \n", entities.All[plr][0].GridPos.X, entities.All[plr][0].GridPos.Y)
	
	var minX f32 = i32.f32(room.Pos.X) * CellSpan
	var minY f32 = i32.f32(room.Pos.Y) * CellSpan
	
	if putInCenter {
		var pos app.Vec2
		pos = app.Vec2{
			X: minX + CellSpanHalf, 
			Y: minY + CellSpanHalf}
		SetPlayerPos(pos)
	}

	// set boundaries (for limiting collision checks) 
	entities.All[plr][0].CellMin.X = minX + WTh
	entities.All[plr][0].CellMin.Y = minY + WTh
	entities.All[plr][0].CellMax.X = minX + CellSpan - WTh
	entities.All[plr][0].CellMax.Y = minY + CellSpan - WTh
	//entities.All[plr][0].Room    = room // CAN'T FIGURE OUT WHY THIS MAKES AVATAR DISAPPEAR 
	
	
	// setup connection flags/boundaries 	
	entities.All[plr][0].ExitWest  = false
	entities.All[plr][0].ExitEast  = false
	entities.All[plr][0].ExitSouth = false
	entities.All[plr][0].ExitNorth = false
	str.print("NEW room has exits in:")
	for i := 0; i < len(room.Exits); i++ {
		str.print(geom.GetTextForDir(room.Exits[i]))
		
		if room.Exits[i] == geom.DIR_LEFT  {
			entities.All[plr][0].ExitWest = true
			entities.All[plr][0].CellMin.X = minX
		}else
		if room.Exits[i] == geom.DIR_RIGHT {
			entities.All[plr][0].ExitEast = true
			entities.All[plr][0].CellMax.X = minX + CellSpan
		}else
		if room.Exits[i] == geom.DIR_DOWN  {
			entities.All[plr][0].ExitSouth = true
			entities.All[plr][0].CellMin.Y = minY
		}else
		if room.Exits[i] == geom.DIR_UP    {
			entities.All[plr][0].ExitNorth = true
			entities.All[plr][0].CellMax.Y = minY + CellSpan
		}
	}
}


// ideally, the below func would be in "sky-taxi/draw.cx", 
// but moving it there would require series of funcs (such as
// (GetRoom(), GetCarvsIdAt(), etc.,
// in order to get all the data you'd need from the "map" package. 
// since ATM, CX 0.7.0 has trouble getting values out of packages 
// via "[package].*".  ATM CX gives lot's of unhelpful errors when 
// such out-of-package data access attempts are made 
func DrawQuads () {
	gl.Translatef(0.0-entities.All[plr][0].x, 0.0-entities.All[plr][0].y, 0.0)
	
	var x f32
	var y f32

	draw.BeginUntexturedQuads()
		var n i32 = len(Carvs)
		//printf("len(Carvs): %d \n", n)
		for i := 0; i < n; i++ {
			var x i32 = Carvs[i].Pos.X
			var y i32 = Carvs[i].Pos.Y
			
			var l f32 = f32.mul(i32.f32(x), CellSpan)
			var b f32 = f32.mul(i32.f32(y), CellSpan)
			var r f32 = l + CellSpan
			var t f32 = b + CellSpan
			
			draw.SetSequentiallyCycledColor(i32.add(x, i32.mul(y, NumCellsAcross)))
			draw.UntexturedQuad(t, r, b, l)
			
			// connection rects 
			draw.SetColorByRGB(0.0, 0.0, 0.0) // black 
			var ex i32 // exit 
			
			for j := 0; j < len(Carvs[i].Exits); j++ {
				var ex i32 = Carvs[i].Exits[j]

				if ex == geom.DIR_LEFT {
					draw.UntexturedQuad(t-WTh, r-WTh, b+WTh, l)
				}else	
				if ex == geom.DIR_RIGHT {
					draw.UntexturedQuad(t-WTh, r,     b+WTh, l+WTh)
				}else	
				if ex == geom.DIR_DOWN {
					draw.UntexturedQuad(t-WTh, r-WTh, b,     l+WTh)
				}else	
				if ex == geom.DIR_UP {
					draw.UntexturedQuad(t,     r-WTh, b+WTh, l+WTh)
				}
			}
		}
	draw.End()
	
	gl.LoadIdentity()
}


func setupPossibleExits () {
	for     iY := 0; iY < NumCellsAcross; iY++ {
		for iX := 0; iX < NumCellsAcross; iX++ {
			var r Cell
			r.CarvsId = app.MAX_I32
			
			if iX > 0 {
				r.Exits = append(
				r.Exits, geom.DIR_LEFT)
			}
			
			if iX < i32.sub(NumCellsAcross, 1) {
				r.Exits = append(
				r.Exits, geom.DIR_RIGHT)
			}
			
			if iY > 0 {
				r.Exits = append(
				r.Exits, geom.DIR_DOWN)
			}
			
			if iY < i32.sub(NumCellsAcross, 1) {
				r.Exits = append(
				r.Exits, geom.DIR_UP)
			}
			
			Cells[iY][iX] = r
			//printf("setupPossibleExits() len(r.Exits): %d \n", len(r.Exits))
			////var poss []i32 = Cells[iY][iX].Exits
			////printf("Cells[iY][iX].Exits: %d \n", len(poss))
		}
	}
}


func getRandomCarvedRoom () (x i32, y i32, c Cell, carvedRoomId i32) {
	carvedRoomId = i32.rand(0, len(Carvs))
	printf("getRandomCarvedRoom: %d \n", carvedRoomId)
	x = Carvs[carvedRoomId].Pos.X
	y = Carvs[carvedRoomId].Pos.Y
	c = Cells[y][x]
}


func carveOutConnectedRooms () {
	var x i32
	var y i32
	var oc Cell  // older cell 
	var orId i32 // older room id 
		
	var numUncarved i32 = i32.rand(1, 5)
	printf("numUncarved: %v \n", numUncarved)
	var n i32 = NumCellsAcross * NumCellsAcross - numUncarved
	
	for i := 0; i < n; i++ {
		if /* 1st room */ i == 0 {
			// make anywhere 
			x = i32.rand(0, NumCellsAcross)
			y = i32.rand(0, NumCellsAcross)
			var cr CarvedRoom = carveOutRoom(x, y, len(Carvs))
	
			
			
			Carvs = append(
			Carvs, cr)
			printf("carved FIRST new @ %d, %d   -   len(Carvs): %d \n", x, y, len(Carvs))
		}else{
			// old cell, old (carved) room id 
			x, y, oc, orId = getRandomCarvedRoom()
			//printf("carveOutConnectedRooms() len(r.Exits): %d \n", len(r.Exits))
			
			for len(oc.Exits) < 1 {
				x, y, oc, orId = getRandomCarvedRoom()
			}
			
			// random (exit) id 
			var ri i32 = i32.rand(0, len(oc.Exits))
			var e /* exit direction (from old) */ i32 = oc.Exits[ri]

			str.print("___________________________________________________________")
			if e == geom.DIR_LEFT {
				drillConnectingWallsAndMaybeRoom(x, y, e, x-1, y)
			}else			
			if e == geom.DIR_RIGHT {
				drillConnectingWallsAndMaybeRoom(x, y, e, x+1, y)
			}else			
			if e == geom.DIR_DOWN {
				drillConnectingWallsAndMaybeRoom(x, y, e, x, y-1)
			}else			
			if e == geom.DIR_UP {
				drillConnectingWallsAndMaybeRoom(x, y, e, x, y+1)
			}

			Carvs[orId].Exits = append(
			Carvs[orId].Exits, e)
			printf("--------------------- Carvs[orId: %d].(len)Exits: %d \n", orId, len(Carvs[orId].Exits))
			//DrawAsciiGraphics()
		}
	}

	//DrawAsciiGraphics()
}


func DrawAsciiGraphics () {
	// XXXXXXXX
	//    ^^ 
	// << 11 >> 
	//    vv
	
	for iY := NumCellsAcross - 1; iY >= 0; iY-- {
		var s0 str
		var s1 str
		var s2 str
		
		for iX := 0; iX < NumCellsAcross; iX++ {
			var carvId i32 = Cells[iY][iX].CarvsId
			
			if carvId == app.MAX_I32 {
				s0 = s0 + "XXXXXXXX  "
				s1 = s1 + "XXXXXXXX  "
				s2 = s2 + "XXXXXXXX  "
			}else{
				var l bool
				var r bool
				var d bool
				var u bool

				for j := 0; j < len(Carvs[carvId].Exits); j++ {
					var ex i32 = Carvs[carvId].Exits[j]

					if ex == geom.DIR_LEFT {
						l = true
					}
					
					if ex == geom.DIR_RIGHT {
						r = true
					}
					
					if ex == geom.DIR_DOWN {
						d = true
					}
					
					if ex == geom.DIR_UP {
						u = true
					}
				}
				
				if u {
					s0 = s0 + "   ^^     "
				}else{
					s0 = s0 + "          "
				}
				
				if l {
					s1 = s1 + "<< "
				}else{
					s1 = s1 + "   "
				}
				
				// id in the middle 
				if carvId < 10 {
					s1 = s1 + " "
				}
				var st str = sprintf("%d", carvId)
				s1 = s1 + st
				
				if r {
					s1 = s1 + " >>  "
				}else{
					s1 = s1 + "     "
				}
				
				if d {
					s2 = s2 + "   VV     "
				}else{
					s2 = s2 + "          "
				}
			}
		}
		
		str.print("\n")
		str.print(s0)
		str.print(s1)
		str.print(s2)
	}
}


func carveOutRoom (x i32, y i32, CarvsId i32) (out CarvedRoom) {
	out.Pos.X = x
	out.Pos.Y = y
	Cells[y][x].CarvsId = CarvsId
}


// old x,y, new x,y 
func drillConnectingWallsAndMaybeRoom (oX i32, oY i32, dir i32, nX i32, nY i32) {
	var nr CarvedRoom // new/target/destination room (to connect to) 
	var newId i32 = Cells[nY][nX].CarvsId

	if newId == app.MAX_I32 {
		str.print("CARVE ROOM")
		
		nr = carveOutRoom(nX, nY, len(Carvs))
		printf("cell @ %d,%d .CarvsId: %d \n", nX, nY, Cells[nY][nX].CarvsId)
		
		nr.Exits = append(
		nr.Exits, geom.GetInvertedDir(dir))
		
		Carvs = append(
		Carvs, nr)
	}else{
		str.print("DRILL WALL")
		
		Carvs[newId].Exits = append(
		Carvs[newId].Exits, geom.GetInvertedDir(dir))
	}
	
	printf("OLD room %d (%d,%d)   %s \n", Cells[oY][oX].CarvsId, oX, oY, geom.GetTextForDir(dir))
	removeExitFromGrid(oX, oY, dir)
	
	printf("NEW room %d (%d,%d)   %s \n", newId, nX, nY, geom.GetTextForDir(geom.GetInvertedDir(dir)))
	removeExitFromGrid(nX, nY, geom.GetInvertedDir(dir))

	printf("--------------------- nr.Exits: %d \n", len(nr.Exits))
	printf("len(Carvs): %d \n", len(Carvs))

	//if newId != app.MAX_I32 {
	//	Carvs[newId] = nr
	//}
}


func removeExitFromGrid (x i32, y i32, dir i32) {
	var log bool = false
	
	if log { 
		str.print("___________________________________________________________")
		printf("removeExitFromGrid(%d, %d, %s) \n", x, y, geom.GetTextForDir(dir))
	}
	
	var c Cell = Cells[y][x]
	// remove from Exits 
	
	if log { printf("------num exits INSIDE-BEFORE: %d \n", len(c.Exits))
	}

	var n i32 = len(c.Exits)
	for i := i32.sub(n, 1); i >= 0; i = i32.sub(i, 1) {
		var s str = geom.GetTextForDir(c.Exits[i])
		
		if c.Exits[i] == dir {
			s = str.concat(s, "  <----------------- REMOVING")
			c.Exits = remove(c.Exits, i)
		}
		
		if log { str.print(s)
		}
	}
	
	if log { printf("------num exits INSIDE-AFTER: %d \n", len(c.Exits))
	}
}
